plugins {
    id 'com.gradleup.shadow'
}

def platform = project.name;

architectury {
    platformSetupLoomIde()
    neoForge()
}

def generatedResources = project(':common').file('src/main/generated')
sourceSets {
    main {
        resources.srcDir generatedResources
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.azuredoom.com/mods' }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
    }

    // For Accessories
    maven { url 'https://maven.wispforest.io/releases' }
    maven { url 'https://maven.su5ed.dev/releases' }
    maven { url 'https://maven.fabricmc.net' }

    mavenLocal()
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
    modImplementation "org.sinytra.forgified-fabric-api:forgified-fabric-api:${project.forgified_fabric_api_version}"

    modImplementation("dev.kosmx.player-anim:player-animation-lib-forge:${project.player_anim_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-${platform}:${project.cloth_config_version}")
    modImplementation("mod.azure.azurelibarmor:azurelibarmor-neo-${project.azure_mc_version}:${project.azure_lib_version}")
    modImplementation("maven.modrinth:tiny-config:${project.tiny_config_version}-${platform}")

    // Accessories - https://docs.wispforest.io/accessories/developer/dev_setup
    modImplementation "maven.modrinth:owo-lib:${project.owo_neo_version}" // AccessoryItem wouldn't compile without this
    modImplementation("io.wispforest:accessories-neoforge:${project.accessories_version}")
    forgeRuntimeLibrary("blue.endless:jankson:1.2.2")
    forgeRuntimeLibrary("io.wispforest:endec:0.1.5.1")
    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:jankson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.2")

    modImplementation("maven.modrinth:spell-power:${project.spell_power_version}-${platform}")
    modImplementation("maven.modrinth:spell-engine:${project.spell_engine_version}-${platform}")
    modImplementation("maven.modrinth:more-rpg-library:${project.more_rpg_library_version}-${platform}")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        def cleanedProps = project.properties.collectEntries { k, v ->
            def s = v?.toString()
            [(k.toString()): (s?.contains('+') ? s.substring(0, s.indexOf('+')) : s)]
        }
        expand(cleanedProps)
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}